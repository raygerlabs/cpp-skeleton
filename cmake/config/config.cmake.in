#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#
#----------------------------------------------------------------------
include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake")
#----------------------------------------------------------------------
set(@PACKAGE_PREFIX@_TARGET_NAMESPACE
  @PROJECT_NAME@)
set(@PACKAGE_PREFIX@_TARGET_PREFIX
  ${@PACKAGE_PREFIX@_TARGET_NAMESPACE}::)
#----------------------------------------------------------------------
get_filename_component(CMAKE_CURRENT_LIST_DIR
  "${CMAKE_CURRENT_LIST_FILE}"
  PATH)
get_filename_component(_INSTALL_PREFIX
  "${CMAKE_CURRENT_LIST_DIR}/@INSTALL_PREFIX_REL2CONFIG_DIR@"
  ABSOLUTE)
#----------------------------------------------------------------------
set(@PACKAGE_PREFIX@_INCLUDE_DIR
  "${_INSTALL_PREFIX}/@PACKAGE_INSTALL_INCLUDEDIR@")
#----------------------------------------------------------------------
if(NOT DEFINED @PACKAGE_PREFIX@_SHARED)
  if(DEFINED @PACKAGE_NAME@_SHARED)
    set (@PACKAGE_PREFIX@_SHARED ${@PACKAGE_NAME@_SHARED})
  else()
    set (@PACKAGE_PREFIX@_SHARED FALSE)
  endif()
endif()
#----------------------------------------------------------------------
if(NOT @PACKAGE_PREFIX@_TARGET)
  if(@PACKAGE_NAME@_TARGET)
    set(@PACKAGE_PREFIX@_TARGET ${@PACKAGE_NAME@_TARGET})
  endif()
endif()
if(NOT TARGET ${@PACKAGE_PREFIX@_TARGET})
  message(FATAL_ERROR "${@PACKAGE_PREFIX@_TARGET} library target "
                       "is missing from @PACKAGE_NAME@ installation!")
endif()
#----------------------------------------------------------------------
if(NOT TARGET ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@)
  if(@PACKAGE_PREFIX@_SHARED)
    add_library(
      ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@
      SHARED
      IMPORTED)
  else()
    add_library(
      ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@
      STATIC
      IMPORTED)
  endif()
  # copy INTERFACE_* properties
  foreach(@PACKAGE_PREFIX@_PROPERTY_NAME IN ITEMS
    COMPILE_DEFINITIONS
    COMPILE_FEATURES
    COMPILE_OPTIONS
    INCLUDE_DIRECTORIES
    LINK_LIBRARIES
    POSITION_INDEPENDENT_CODE)
    get_target_property(
      @PACKAGE_PREFIX@_PROPERTY_VALUE ${@PACKAGE_PREFIX@_TARGET}
      INTERFACE_${@PACKAGE_PREFIX@_PROPERTY_NAME})
    if (@PACKAGE_PREFIX@_PROPERTY_NAME)
      set_target_properties(
        ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@
        PROPERTIES
        INTERFACE_${@PACKAGE_PREFIX@_PROPERTY_NAME}
        "${@PACKAGE_PREFIX@_PROPERTY_VALUE}")
    endif()
  endforeach()
  get_target_property(
    @PACKAGE_PREFIX@_CONFIGURATIONS
    ${@PACKAGE_PREFIX@_TARGET}
    IMPORTED_CONFIGURATIONS)
  set_target_properties(
    ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@
    PROPERTIES
    IMPORTED_CONFIGURATIONS
    "${@PACKAGE_PREFIX@_CONFIGURATIONS}")
  foreach(@PACKAGE_PREFIX@_PROPERTY_NAME IN ITEMS
    IMPLIB
    LOCATION
    LINK_DEPENDENT_LIBRARIES
    LINK_INTERFACE_LIBRARIES
    LINK_INTERFACE_LANGUAGES
    LINK_INTERFACE_MULTIPLICITY
    NO_SONAME
    SONAME)
    foreach(@PACKAGE_PREFIX@_CONFIG IN LISTS
      @PACKAGE_PREFIX@_CONFIGURATIONS)
      get_target_property(
        @PACKAGE_PREFIX@_PROPERTY_VALUE
        ${@PACKAGE_PREFIX@_TARGET}
        IMPORTED_${@PACKAGE_PREFIX@_PROPERTY_NAME}_\
        ${@PACKAGE_PREFIX@_CONFIG})
      if(@PACKAGE_PREFIX@_PROPERTY_VALUE)
        set_target_properties(
          ${@PACKAGE_PREFIX@_TARGET_PREFIX}@PACKAGE_NAME@
          PROPERTIES
          IMPORTED_${@PACKAGE_PREFIX@_PROPERTY_NAME}_\
          ${@PACKAGE_PREFIX@_CONFIG}
          "${@PACKAGE_PREFIX@_PROPERTY_VALUE}")
      endif()
    endforeach()
  endforeach()
endif()
set(@PACKAGE_PREFIX@_LIBRARIES "${@PACKAGE_PREFIX@_TARGET}")
#----------------------------------------------------------------------
#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#
